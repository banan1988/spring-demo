plugins {
    // springfox-swagger2:2.9.2 is not compatible with version 2.2.0.RELEASE so we cannot upgrade it :(
    id 'org.springframework.boot' version '2.1.9.RELEASE'
    id 'java'
    id 'idea'

    // code coverage
    id 'jacoco'
    // code quality
    id "org.sonarqube" version "2.8"

    // Generate Git Information
    id "com.gorylenko.gradle-git-properties" version "2.2.0"

    // Keep dependencies up to date
    id "com.github.ben-manes.versions" version "0.27.0"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'jacoco'

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_12

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
}

configurations.all {
    transitive = true
    exclude group: "junit", module: "junit"
}

repositories {
    mavenCentral()
}

idea {
    module {
        sourceDirs += file('jenkinsfile')
        downloadJavadoc = true
        downloadSources = true
    }
}

dependencies {
    // generated
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-ldap'
    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springframework.boot:spring-boot-starter-quartz'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.liquibase:liquibase-core'

    compileOnly 'org.projectlombok:lombok'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'org.postgresql:postgresql'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'

    /************************
     added by me
     ************************/

    // swagger
    implementation "io.springfox:springfox-swagger2:${swaggerVersion}"
    implementation "io.springfox:springfox-swagger-ui:${swaggerVersion}"

    // junit5
    testImplementation "org.junit.jupiter:junit-jupiter:${junit5Version}"

    // templates
    compile 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect:2.4.1'
    compile 'org.thymeleaf.extras:thymeleaf-extras-springsecurity5:3.0.4.RELEASE'

    // CSS styles and JS scripts
    compile 'org.webjars:bootstrap:4.3.1'
    compile 'org.webjars:jquery:3.4.1'
    compile 'org.webjars:angularjs:1.7.7'

    // LDAP authentication
    compile("org.springframework.ldap:spring-ldap-core")
    compile("org.springframework.security:spring-security-ldap")
    compile("com.unboundid:unboundid-ldapsdk")
}

test {
    useJUnitPlatform()
}
// after test task gradle always execute jacoco's tasks
test.finalizedBy jacocoTestReport
test.finalizedBy jacocoTestCoverageVerification

jacoco {
    toolVersion = "${jacocoVersion}"
}

jacocoTestReport {
    reports {
        csv.enabled false

        xml.enabled true
        xml.destination file("${buildDir}/jacoco/test.xml")

        html.enabled true
        html.destination file("${buildDir}/jacocoHtml")
    }
}
project.tasks["jacocoTestReport"].dependsOn "test"

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = Double.parseDouble("${jacocoQualityGate}")
            }
        }
    }
}
project.tasks["jacocoTestCoverageVerification"].dependsOn "jacocoTestReport"

def branchName = System.getenv('BRANCH_NAME') ? System.getenv('BRANCH_NAME') : System.getProperty('user.name')
println("Branch name: ${branchName}")
sonarqube {
    properties {
        property "sonar.sourceEncoding", "${sonarSourceEncoding}"
        property "sonar.host.url", "${sonarHostUrl}"
        property "sonar.login", "${sonarLogin}"
        // Branch analysis is available in Developer Edition and above
//        property "sonar.branch.name", "${branchName}"
        property "sonar.jacoco.reportPaths", "${buildDir}/jacoco.exec"
        property "sonar.junit.reportPaths", "${buildDir}/test-results/**/*.xml, **/${buildDir}/test-results/**/*.xml"
    }
}
project.tasks["sonarqube"].dependsOn "jacocoTestReport"
project.tasks["sonarqube"].dependsOn "jacocoTestCoverageVerification"

// Generate Build Information
springBoot {
    buildInfo {
        properties {
            additional = [
                    'description': 'description'
            ]
        }
    }
}
// HACK - Up to date build info needs to be generated after processResources
bootBuildInfo.dependsOn(processResources)
generateGitProperties.dependsOn(processResources)
